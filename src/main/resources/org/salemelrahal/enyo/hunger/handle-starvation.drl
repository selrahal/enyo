package org.salemelrahal.enyo.hunger;

import bothack.actions.Actions;
import bothack.actions.ActionsComplex;
import bothack.bot.IGame;
import bothack.bot.items.IItem;
import bothack.bot.Hunger;
import java.util.Map.Entry;
import org.salemelrahal.enyo.action.PrioritizedAction;
import org.salemelrahal.enyo.item.ItemInInventory;
import org.salemelrahal.enyo.item.ItemOnTile;
import org.salemelrahal.enyo.state.InShop;

global org.slf4j.Logger logger;

rule "Eat food in inventory when weak"
when
	$game : IGame($player : player, player.hunger() == Hunger.WEAK)
	ItemInInventory($slot : slot, $item : item, $player.canEat(item), !item.name().contains("tin"))
then
	insert(PrioritizedAction.create(20 , Actions.Eat($slot), "eating " + $item.name() + " in slot " + $slot + " because weak"));
end

rule "Pray when fainting"
when
	IGame(player.hunger() == Hunger.FAINTING, canPray())
then
	insert(PrioritizedAction.create(100 , Actions.Pray(), "praying because fainting"));
end

rule "Eat food outside of a shop"
when
	IGame($player : player, player.hunger() != Hunger.SATIATED)
	ItemOnTile($corpse : item, $player.canEat(item), !item.isCorpse(), item.name() != "tin") //refactor this corpse logic, and tin logic
	not InShop()
then
	insert(PrioritizedAction.create(2 , Actions.Eat($corpse.label()), "eating edible item outside of a shop, " + $corpse.type().name() + "[" + $corpse.name() + "]"));
end


rule "Eat fresh corpse outside of a shop"
when
	$game : IGame($player : player, player.hunger() != Hunger.SATIATED, $tile : currentLevel.at(player))
	ItemOnTile($corpse : item, $player.canEat(item), item.isCorpse(), $game.isCorpseFresh($tile, item)) //refactor this corpse logic
	not InShop()
then
	insert(PrioritizedAction.create(2 , Actions.Eat($corpse.label()), "eating fresh corpse outside of a shop, " + $corpse.type().name() + "[" + $corpse.name() + "]"));
end