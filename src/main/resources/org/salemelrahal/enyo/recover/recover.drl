package org.salemelrahal.enyo.hunger;

import bothack.actions.Actions;
import bothack.actions.ActionsComplex;
import bothack.bot.IGame;
import bothack.bot.items.IItem;
import bothack.bot.Hunger;
import java.util.Map.Entry;
import org.salemelrahal.enyo.action.PrioritizedAction;
import org.salemelrahal.enyo.item.ItemInInventory;
import org.salemelrahal.enyo.item.ItemOnTile;
import org.salemelrahal.enyo.state.LowHealth;
import org.salemelrahal.enyo.state.MediumHealth;
import org.salemelrahal.enyo.state.OnElbereth;
import org.salemelrahal.enyo.state.OnEngravableTile;
import bothack.bot.items.BUC;

global org.slf4j.Logger logger;

rule "Recover when low health when there are no monsters around"
when
	MediumHealth()
	not Enemy()
then
	insertLogical(PrioritizedAction.create(20 , Actions.Search(), "waiting to heal up "));
end

rule "Drink potion when in trouble"
when
	LowHealth()
	Enemy(!respectsElbereth())
	ItemInInventory($slot : slot, item.name().contains("potion of full healing"), !BUC.CURSED.equals(item.buc()))
then
	insertLogical(PrioritizedAction.create(22 , Actions.Quaff($slot), "quaffing !c||u potion of full healing"));
end


rule "Recover when low health in a safe spot"
when
	OnElbereth()
	LowHealth()
	not Enemy(!respectsElbereth())
then
	insertLogical(PrioritizedAction.create(21 , Actions.Search(), "waiting to heal up, on elbereth "));
end

rule "Engrave with wand of fire when in danger"
when
	IGame(canEngrave())
	ItemInInventory($slot : slot, item.name().contains("wand of fire"))
	OnEngravableTile()
	not OnElbereth()
	LowHealth()
	Enemy(respectsElbereth())
	not Enemy(!respectsElbereth())
then
	insertLogical(PrioritizedAction.create(24 , Actions.EngraveOverwriting($slot, "Elbereth"), "Engraving elbereth with wand of fire "));
end


rule "Engrave with finger when in danger"
when
	IGame(canEngrave())
	OnEngravableTile()
	not OnElbereth()
	LowHealth()
	Enemy(respectsElbereth())
	not Enemy(!respectsElbereth())
then
	insertLogical(PrioritizedAction.create(23 , Actions.EngraveOverwriting('-', "Elbereth"), "Engraving elbereth with finger "));
end

