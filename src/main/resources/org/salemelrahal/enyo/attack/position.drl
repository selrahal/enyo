package org.salemelrahal.enyo.attack;

import bothack.actions.Actions;
import org.salemelrahal.enyo.action.MoveToAdvantageousTile;
import org.salemelrahal.enyo.action.monster.MoveTowardsEnemy;
import org.salemelrahal.enyo.enemy.Enemy;
import bothack.bot.IGame;
import bothack.bot.Hunger;
import bothack.bot.Direction;
import bothack.bot.monsters.IMonster;
import bothack.bot.Position;
import org.salemelrahal.enyo.util.MapUtil;
import org.salemelrahal.enyo.state.OnElbereth;
import org.salemelrahal.enyo.state.OnEngravableTile;
import org.salemelrahal.enyo.item.ItemInInventory;
import org.salemelrahal.enyo.fact.PathToAdvantageousTile;
import org.salemelrahal.enyo.state.OnAdvantageousTile;

global org.slf4j.Logger logger;


rule "Mark advantageous tile"
when
	IGame(MapUtil.isAdvantageous(this, currentLevel.at(player)))
then
	insertLogical(new OnAdvantageousTile());
end


rule "Move to advantageous tile when about to face many enemies" 
when
	IGame($player : player)
	$first : Enemy(isAwake())
	$second : Enemy(isAwake(),  this != $first)
	not OnElbereth()
	not OnAdvantageousTile()
	PathToAdvantageousTile($path : path != null)
then
	insertLogical(new MoveToAdvantageousTile($player, $path));
end

rule "Identical directions make identical moves"
when
	$first : MoveToAdvantageousTile($direction : direction)
	$second : MoveToAdvantageousTile(direction == $direction, this != $first)
then
	delete($second);
end

rule "Closer advantageous tiles are better"
when
	$closer : MoveToAdvantageousTile($distance : distance)
	$farther : MoveToAdvantageousTile(distance > $distance)
then
	delete($farther);
end

rule "Move to advantageous tile instead of attacking"
when
	$attack : Attack()
	$move : MoveToAdvantageousTile()
then
	delete($attack);
end

rule "Move to advantageous tile instead of approaching enemies"
when
	$approach : MoveTowardsEnemy()
	$move : MoveToAdvantageousTile()
then
	delete($approach);
end