package org.salemelrahal.enyo.attack;

import bothack.bot.IGame;
import org.salemelrahal.enyo.action.monster.MoveTowardsEnemy;
import org.salemelrahal.enyo.action.monster.Attack;
import org.salemelrahal.enyo.enemy.Enemy;
import bothack.bot.monsters.IMonster;
import bothack.actions.Actions;
import bothack.bot.Direction;
import bothack.bot.dungeon.Feature;

global org.slf4j.Logger logger;

rule "Move towards marked enemy that is not adjacent"
when
	$enemy : Enemy(isMarked(), !isAdjacent(), $name : getName(), $path : getPathTo() != null) //monsters seen via telepathy might not have a pathTo
then
	insertLogical(new MoveTowardsEnemy($enemy, $path));
end

rule "Debug MoveTowardsEnemy"
when
	MoveTowardsEnemy($enemy : target, $difficulty : target.getDifficulty(), $distance : target.getDistance())
then
	logger.info("**MoveTowardsEnemy" + $enemy.getPosition() + " , dif=" + $difficulty + " dis=" + $distance);
end

rule "Move to eliminate the weaklings!" //Maybe figure out a ration of difficulty/distance that provides best target?
when
	MoveTowardsEnemy($difficulty : target.getDifficulty())
	$stronger : MoveTowardsEnemy(target.getDifficulty() > $difficulty)
then
	delete($stronger);
end

rule "Move to the closest enemies" //Maybe figure out a ration of difficulty/distance that provides best target?
when
	MoveTowardsEnemy($distance : target.getDistance())
	$farther : MoveTowardsEnemy(target.getDistance() > $distance)
then
	delete($farther);
end