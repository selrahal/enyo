package org.salemelrahal.drhack.attack;

import bothack.actions.Actions;
import org.salemelrahal.drhack.enemy.Enemy;
import bothack.bot.IGame;
import bothack.bot.Hunger;
import bothack.bot.Direction;
import bothack.bot.monsters.IMonster;
import bothack.bot.Position;
import org.salemelrahal.drhack.action.PrioritizedAction;
import org.salemelrahal.drhack.util.MapUtil;
import org.salemelrahal.drhack.state.OnElbereth;
import org.salemelrahal.drhack.state.OnEngravableTile;
import org.salemelrahal.drhack.item.ItemInInventory;
import org.salemelrahal.drhack.fact.PathToAdvantageousTile;
import org.salemelrahal.drhack.item.RangedWeapon;

global org.slf4j.Logger logger;

rule "Melee attack adjacent target enemy"
when
	$game : IGame($player : player)
	Enemy($monster : target, isMarked(), isAdjacent(), $name : getName())
then
	insertLogical(PrioritizedAction.create(22 , Actions.Move(Direction.towards($player, $monster)), "attack adjacent marked monster " + $name));
end


rule "Range attack target enemy"
when
	$game : IGame($player : player)
	Enemy($monster : target, isRangeMarked(), $name : getName())
	RangedWeapon($slot : slot)
then
	insertLogical(PrioritizedAction.create(16 , Actions.Throw($slot, Direction.towards($player, $monster)), "throwing weapon at range marked monster " + $name));
end